plugins {
    id 'com.android.application' version '8.0.2' apply false
    id 'com.android.library' version '8.0.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.20' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.8.20' apply false
}

subprojects {
    if (name == 'app') {
        apply plugin: 'com.android.application'
    } else if (name.endsWith('android')
            || name.endsWith('data')
            || name.endsWith('ui')
    ) {
        apply plugin: 'com.android.library'
    }

    if (plugins.findPlugin('com.android.base') != null) {
        apply plugin: 'kotlin-android'

        android {
            compileSdkVersion 33

            defaultConfig {
                minSdkVersion 24
                targetSdkVersion 33
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            buildFeatures {
                viewBinding = true
            }
        }
    } else {
        apply plugin: 'java-library'
        apply plugin: 'kotlin'

        java {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
    }
}
