import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'com.android.application' version '8.0.2' apply false
    id 'com.android.library' version '8.0.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.20' apply false
    id 'org.jetbrains.kotlin.jvm' version '1.8.20' apply false
}

subprojects {
    def moduleRelativePath = projectDir.toString() - rootDir.toString()

    if (name == 'app') {
        apply plugin: 'com.android.application'
    } else if (name == 'android'
            || name == 'di'
            || name == 'data'
            || name == 'ui'
    ) {
        apply plugin: 'com.android.library'
    }

    if (plugins.findPlugin('com.android.base') != null) {
        apply plugin: 'kotlin-android'

        android {
            namespace 'dev.supasintatiyanupanwong.apps.android.bpi' +
                    moduleRelativePath.replace(File.separator, '.')
            compileSdkVersion 33

            defaultConfig {
                minSdkVersion 24
                targetSdkVersion 33
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }

            buildFeatures {
                viewBinding = true
            }
        }
    } else {
        apply plugin: 'java-library'
        apply plugin: 'kotlin'

        java {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }

    if (!moduleRelativePath.startsWith(File.separator + 'base' + File.separator)) {
        dependencies {
            if (name == 'app' || name == 'data') {
                api project(':base:data')
            }
            if (name == 'app' || name == 'di') {
                api project(':base:di')
            }
            if (name == 'app' || name == 'domain') {
                api project(':base:domain')
            }
            if (name == 'app' || name == 'ui') {
                api project(':base:ui')
            }
        }
    }

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
    }
}
